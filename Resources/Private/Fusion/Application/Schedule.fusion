prototype(Sitegeist.Bitzer:Application.Schedule) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <Sitegeist.Bitzer:FlashMessages />
        <table class="neos-table">
            <thead>
                <tr>
                    <th></th>
                    <th>{q(labels).get('task.scheduledTime.label')}</th>
                    <th>{q(labels).get('task.actionStatus.label')}</th>
                    <th>{q(labels).get('task.type.label')}</th>
                    <th>{q(labels).get('task.properties.description.label')}</th>
                    <th>{q(labels).get('task.agent.label')}</th>
                    <th>{q(labels).get('task.object.label')}</th>
                    <th><div class="neos-pull-right">{q(labels).get('actions.label')}</div></th>
                </tr>
            </thead>
            <tbody>
                <Neos.Fusion:Loop items={tasks} itemName="task">
                    <tr>
                        <td><i class={'fas fa-' + task.image + ' icon-white'}></i></td>
                        <td>{Date.format(task.scheduledTime, 'Y-m-d H:i')}</td>
                        <td>{q(labels).get('actionStatusType.' + String.toString(task.actionStatus) + '.label')}</td>
                        <td>{task.shortType}</td>
                        <td>{task.description}</td>
                        <td>{task.agent.label}</td>
                        <td>{task.object.label}</td>
                        <td class="neos-action">
                            <div class="neos-pull-right">
                                <Sitegeist.Bitzer:ActionLink actionName="editTask" actionArguments={{taskIdentifier: String.toString(task.identifier)}} icon="pencil-alt" />
                                <Sitegeist.Bitzer:CommandForm commandName="completeTask" commandArguments={{taskIdentifier: String.toString(task.identifier)}} icon="check" />
                                <Sitegeist.Bitzer:CommandForm commandName="cancelTask" commandArguments={{taskIdentifier: String.toString(task.identifier)}} icon="times" />
                            </div>
                        </td>
                    </tr>
                </Neos.Fusion:Loop>
            </tbody>
        </table>
        <div class="neos-footer">
            <form action="/neos/management/task" method="get">
                <input type="hidden" name="moduleArguments[@package]" value="Sitegeist.Bitzer" />
                <input type="hidden" name="moduleArguments[@subpackage]" value="Application" />
                <input type="hidden" name="moduleArguments[@controller]" value="Bitzer" />
                <input type="hidden" name="moduleArguments[@action]" value="prepareTask" />
                <input type="hidden" name="moduleArguments[@format]" value="html" />
                <select id="taskClassName" name="moduleArguments[taskClassName]">
                    <Neos.Fusion:Loop items={taskClassNames} itemName="taskClassName">
                        <option value={taskClassName.identifier}>{taskClassName.label}</option>
                    </Neos.Fusion:Loop>
                </select>
                <input class="neos-button neos-button-primary" type="submit" name="" value={q(labels).get('actions.prepareTask.label')} />
            </form>
        </div>
        <Sitegeist.Bitzer:ToggleScript />
    `
}
