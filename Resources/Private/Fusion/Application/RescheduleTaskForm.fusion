prototype(Sitegeist.Bitzer:RescheduleTaskForm) < prototype(Neos.Fusion:Component) {
    task = null
    constraintCheckResult = null

    @context {
        rescheduleUri = Neos.Fusion:UriBuilder {
            package = 'Sitegeist.Bitzer'
            subpackage = 'Application'
            controller = 'Bitzer'
            action = 'rescheduleTask'
        }
        labels = ${Bitzer.Labels.forRescheduleTaskForm()}
    }

    renderer = afx`
        <form action={rescheduleUri} method="post">
            <fieldset class="neos-span5">
                <legend>{q(labels).get('rescheduleTask.label')}</legend>
                <input type="hidden" name="__csrfToken" value={${Security.csrfToken()}} />
                <input type="hidden" name="moduleArguments[taskIdentifier]" value={props.task.identifier.value} />
                <div class={'neos-control-group' + (constraintCheckResult.hasFailedAtPath('scheduledTime') ? ' neos-error' : '')}>
                    <label class="neos-control-label" for="scheduledTime.date">{q(labels).get('task.scheduledTime.date')}*</label>
                    <div class="neos-controls neos-controls-row">
                        <input type="text" required="required" class="neos-span4" id="scheduledTime.date" name="moduleArguments[scheduledTime][date]" placeholder={${Date.format(Date.now(), 'Y-m-d')}} value={${Date.format(task.scheduledTime, 'Y-m-d')}} />
                    </div>
                    <Sitegeist.Bitzer:Application.ConstraintCheckMessages constraintCheckResult={props.constraintCheckResult} path="scheduledTime" />
                </div>
                <div class="neos-control-group">
                    <label class="neos-control-label" for="scheduledTime.time">{q(labels).get('task.scheduledTime.time')}</label>
                    <div class="neos-controls neos-controls-row">
                        <input type="text" class="neos-span4" id="scheduledTime.time" name="moduleArguments[scheduledTime][time]" placeholder={${Date.format(Date.now(), 'H:i:s')}} value={${Date.format(task.scheduledTime, 'H:i:s')}}/>
                    </div>
                </div>
                <input class="neos-button neos-button-primary" type="submit" name="" value={q(labels).get('rescheduleTask.label')} />
            </fieldset>
        </form>
    `
}
