prototype(Sitegeist.Bitzer:CommandForm) < prototype(Neos.Fusion:Component) {
    commandName = ''
    commandArguments = Neos.Fusion:DataStructure
    icon = ''
    target = '_self'

    renderer = Neos.Fusion:Tag {
        tagName = 'form'

        attributes = Neos.Fusion:Attributes {
            method = 'POST'
            data-neos-toggle = 'tooltip'
            data-original-title = ${Translation.translate(props.commandName + '.label', null, [], 'Module.Bitzer', 'Sitegeist.Bitzer')}
            style = 'display: inline-block'
            target = ${props.target}

            action = Sitegeist.Bitzer:CommandUri {
                commandName = ${props.commandName}
                commandArguments = ${props.commandArguments}
            }
        }
        content = afx`
            <input type="hidden" name="__csrfToken" value={${Security.csrfToken()}} />
            <button type="submit">
                <i class={${'fas fa-' + props.icon + ' icon-white'}}></i>
            </button>
        `
    }
}
