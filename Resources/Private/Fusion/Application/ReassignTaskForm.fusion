prototype(Sitegeist.Bitzer:ReassignTaskForm) < prototype(Neos.Fusion:Component) {
    task = null
    agents = ${[]}
    constraintCheckResult = null

    @context {
        reassignUri = Neos.Fusion:UriBuilder {
            package = 'Sitegeist.Bitzer'
            subpackage = 'Application'
            controller = 'Bitzer'
            action = 'reassignTask'
        }
        labels = ${Bitzer.Labels.forReassignTaskForm()}
    }

    renderer = afx`
        <form action={reassignUri} method="post">
            <fieldset class="neos-span5">
                <legend>{q(labels).get('reassignTask.label')}</legend>
                <input type="hidden" name="__csrfToken" value={${Security.csrfToken()}} />
                <input type="hidden" name="moduleArguments[taskIdentifier]" value={props.task.identifier.value} />
                <div class={'neos-control-group' + (constraintCheckResult.hasFailedAtPath('agent') ? ' neos-error' : '')}>
                    <label class="neos-control-label" for="title">{q(labels).get('task.agent.label')}*</label>
                    <div class="neos-controls neos-controls-row">
                        <select id="agent" name="moduleArguments[agent]">
                            <Neos.Fusion:Loop items={props.agents} itemName="agent">
                                <option @if.1={agent.identifier.string == props.task.agent.identifier.string} value={agent.identifier.string} selected="selected">{agent.label}</option>
                                <option @if.1={agent.identifier.string != props.task.agent.identifier.string} value={agent.identifier.string}>{agent.label}</option>
                            </Neos.Fusion:Loop>
                        </select>
                    </div>
                </div>
                <input class="neos-button neos-button-primary" type="submit" name="" value={q(labels).get('reassignTask.label')} />
            </fieldset>
        </form>
    `
}
