prototype(Sitegeist.Bitzer:Application.Index) < prototype(Neos.Fusion:Component) {
    @context.prepareTaskUri = afx`<Sitegeist.Bitzer:ActionUri action="prepareTask" />`
    @context.scheduleWidget = ${Widget.forSchedule(this)}
    @context.myScheduleWidget = ${Widget.forMySchedule(this)}
    widgets = Neos.Fusion:DataStructure {
        schedule {
            icon = ${scheduleWidget.icon}
            uri = ${scheduleWidget.uri}
            title = ${scheduleWidget.title}
            description = ${scheduleWidget.description}
            footer = afx`<form action="/neos/management/task" method="get">
                <input type="hidden" name="moduleArguments[@package]" value="Sitegeist.Bitzer" />
                <input type="hidden" name="moduleArguments[@subpackage]" value="Application" />
                <input type="hidden" name="moduleArguments[@controller]" value="Bitzer" />
                <input type="hidden" name="moduleArguments[@action]" value="prepareTask" />
                <input type="hidden" name="moduleArguments[@format]" value="html" />
                <select id="taskClassName" name="moduleArguments[taskClassName]">
                    <Neos.Fusion:Loop items={taskClassNames} itemName="taskClassName">
                        <option value={taskClassName.identifier}>{taskClassName.label}</option>
                    </Neos.Fusion:Loop>
                </select>
                <input class="neos-button neos-button-primary" type="submit" name="" value={scheduleWidget.footer} />
            </form>`
        }
        mySchedule {
            icon = ${myScheduleWidget.icon}
            uri = ${myScheduleWidget.uri}
            title = ${myScheduleWidget.title}
            description = ${myScheduleWidget.description}
            footer = ''
        }
    }

    renderer = afx`
        <Sitegeist.Bitzer:FlashMessages />
        <div class="neos-row-fluid">
            <Neos.Fusion:Loop items={props.widgets} itemName="widget">
                <div class="neos-span4">
                    <div class="widget has-footer">
                        <div class="widget-header">
                            <i class={'neos-pull-left ' + widget.icon}></i>
                            <h3>
                                <a href={widget.uri}>{widget.title}</a>
                            </h3>
                        </div>
                        <div class="widget-body">
                            <p>{widget.description}</p>
                        </div>
                        <div class="widget-footer" @if.1={widget.footer}>{widget.footer}</div>
                    </div>
                </div>
            </Neos.Fusion:Loop>
        </div>`
}
