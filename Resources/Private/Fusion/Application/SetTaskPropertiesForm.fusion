prototype(Sitegeist.Bitzer:SetTaskPropertiesForm) < prototype(Neos.Fusion:Component) {
    task = null
    constraintCheckResult = null

    @context {
        setTaskPropertiesUri = Neos.Fusion:UriBuilder {
            package = 'Sitegeist.Bitzer'
            subpackage = 'Application'
            controller = 'Bitzer'
            action = 'setTaskProperties'
        }
        labels = ${Bitzer.Labels.forSetTaskPropertiesForm()}
    }

    renderer = afx`
        <form action={setTaskPropertiesUri} method="post">
            <fieldset class="neos-span5">
                <legend>{q(labels).get('setTaskProperties.label')}</legend>
                <input type="hidden" name="__csrfToken" value={${Security.csrfToken()}} />
                <input type="hidden" name="moduleArguments[taskIdentifier]" value={props.task.identifier.value} />
                <div class={'neos-control-group' + (constraintCheckResult.hasFailedAtPath('properties.description') ? ' neos-error' : '')}>
                    <label class="neos-control-label" for="title">{q(labels).get('task.properties.description.label')}*</label>
                    <div class="neos-controls neos-controls-row">
                        <textarea id="description" required="required" class="neos-span4" rows="3"
                            name="moduleArguments[properties][description]"
                            placeholder={q(labels).get('task.properties.description.placeholder')}
                        >{props.task.description}</textarea>
                    </div>
                    <Sitegeist.Bitzer:Application.ConstraintCheckMessages constraintCheckResult={props.constraintCheckResult} path="description" />
                </div>
                <input class="neos-button neos-button-primary" type="submit" name="" value={q(labels).get('setTaskProperties.label')} />
            </fieldset>
        </form>`
}
